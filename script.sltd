. Solitude supports variables.
var a=Hello
var b=World
var c=3

...
Here is a multiline comment.
The dots must be on their own line.
...

. Solitude also supports redefining variables.
var c=$a

. Printing has no function, just type what you want to print, with `$` before the variable name.
`c` is $c\n

. You can also remove variables.
-c

...
You can also get user input.
The `\x20` is a space character.
...
input name -> Type your name:\x20

. ANSI escape codes are supported.
Hello, \033[1m$name\033[0m!\n

. You can also define functions.
func greet
    Hello, $b!\n
cnuf

var c=2

. You can also use if statements.
if $c>=2
    call greet
fi

. Solitude also supports threading.
func a
    \n
    Hello from function "a"!
    \n\n
cnuf

func b
    Hello from function "b"!\n
cnuf

!!
    {
        call a
        call a
    }
    {
        call b
    }
??
